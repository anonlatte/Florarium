plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "org.jlleitschuh.gradle.ktlint"
    id "com.github.ben-manes.versions" version "0.39.0"
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.anonlatte.florarium"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                if (variant.buildType.name == "debug") {
                    outputFileName = "app-debug.apk"
                } else {
                    outputFileName = "Florarium-${versionName}-${variant.buildType.name}.apk"
                }
            }
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    kapt "androidx.room:room-compiler:$roomVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "androidx.work:work-runtime-ktx:$workVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    implementation "androidx.recyclerview:recyclerview-selection:$recyclerViewSelectionVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$navigationVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifeCycleVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    debugImplementation "androidx.fragment:fragment-testing:$fragmentVersion"

    androidTestImplementation "androidx.arch.core:core-testing:$archCoreVersion"
    androidTestImplementation "androidx.navigation:navigation-testing:$navigationVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.ext:junit:$junitExtVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
}

ktlint {
    android.set(true)
    outputColorName.set("RED")
}